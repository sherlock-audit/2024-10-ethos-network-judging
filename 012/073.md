Dapper Raisin Locust

High

# `profileIdByAddress` not cleared in `deleteAddressAtIndex`

## Vulnerability Details
Users can remove an address registered to their Ethos profile by calling [deleteAddressAtIndex](https://github.com/sherlock-audit/2024-10-ethos-network/blob/db37b9dc2b792e245eb683d8a956bcb7ef2f1a27/ethos/packages/contracts/contracts/EthosProfile.sol#L415-L438). However, this function does not reset the `profileIdByAddress` mapping, leaving the removed address still associated with the Ethos profile:
```solidity
// EthosPorfile

function deleteAddressAtIndex(uint256 addressIndex) external whenNotPaused {
    uint256 profileId = profileIdByAddress[msg.sender];
    (bool verified, bool archived, bool mock) = profileStatusById(profileId);
    if (!verified) {
      revert ProfileNotFoundForAddress(msg.sender);
    }
    if (archived || mock) {
      revert ProfileAccess(profileId, "Profile is archived");
    }

    address[] storage addresses = profiles[profileId].addresses;
    address[] storage removedAddresses = profiles[profileId].removedAddresses;
    if (addresses.length <= addressIndex) {
      revert InvalidIndex();
    }

    address addressStr = addresses[addressIndex];
    isAddressCompromised[addressStr] = true;
    _addressShouldDifferFromSender(addressStr);

    _deleteAddressAtIndexFromArray(addressIndex, addresses, removedAddresses);

>>>    // @audit profileIdByAddress[addressStr] is not reset
    emit AddressClaim(profileId, addressStr, AddressClaimStatus.Unclaimed);
  }
```
As a result, reviews on the removed address will still impact the Ethos profile’s credibility score, as the removed address remains associated with the profile:
```solidity
// EthosReview

function addReview( Score score, address subject, address paymentToken, string calldata comment, string calldata metadata, AttestationDetails calldata attestationDetails
) external payable whenNotPaused {
  // --SNIP

  if (subject != address(0)) {
>>>    mockId = ethosProfile.profileIdByAddress(subject);
  // @audit mockId points to the ethos profile
    mockId = _addReview(mockId, subject, false, 0x0, ethosProfile);
  }
}
```

## Impact
Reviews on addresses that have been removed from an Ethos profile continue to affect the profile’s credibility score.

## Mitigation

Consider resetting `profileIdByAddress` in `deleteAddressAtIndex`:
```diff
function deleteAddressAtIndex(uint256 addressIndex) external whenNotPaused {
    // --SNIP

    address addressStr = addresses[addressIndex];
    isAddressCompromised[addressStr] = true;
    _addressShouldDifferFromSender(addressStr);

    _deleteAddressAtIndexFromArray(addressIndex, addresses, removedAddresses);

+   profileIdByAddress[addressStr] = 0;
    emit AddressClaim(profileId, addressStr, AddressClaimStatus.Unclaimed);
}
```