Mammoth Teal Aardvark

Medium

# After claiming the attestation, the reviews for the attestation is stuck on old mock profile

### Summary

In current system design of the Ethos Social Network, people can write review to unclaimed attestations. Those attestations can also be claimed by a valid user. But the reviews are stuck on old mock profile.

### Root Cause

In [Review Contract](https://github.com/sherlock-audit/2024-10-ethos-network/blob/db37b9dc2b792e245eb683d8a956bcb7ef2f1a27/ethos/packages/contracts/contracts/EthosReview.sol#L173C1-L219C4), while adding a review there are several checks such as existence of the attestation. If the attestation is not claimed by a profile, it creates a mock profile and add the review to that mock profile.

<details>
    <summary>addReview function code snippet</summary>

```solidity
  function addReview(
    Score score,
    address subject,
    address paymentToken,
    string calldata comment,
    string calldata metadata,
    AttestationDetails calldata attestationDetails
  ) external payable whenNotPaused {
    _validateReviewDetails(subject, attestationDetails);

    IEthosProfile ethosProfile = _getEthosProfile();
    bytes32 attestationHash;
    uint256 mockId;
    if (subject != address(0)) {
      mockId = ethosProfile.profileIdByAddress(subject);
      mockId = _addReview(mockId, subject, false, 0x0, ethosProfile);
    } else {
      // convert the service/account to a hash as an identifier
      attestationHash = _getEthosAttestation().getServiceAndAccountHash(
        attestationDetails.service,
        attestationDetails.account
      );
      mockId = ethosProfile.profileIdByAttestation(attestationHash);
@>    mockId = _addReview(mockId, subject, true, attestationHash, ethosProfile);
    }

    uint256 authorProfileId = ethosProfile.verifiedProfileIdForAddress(msg.sender);
    reviewIdsByAuthorProfileId[authorProfileId].push(reviewCount);

    _handlePayment(paymentToken);

    reviews[reviewCount] = Review({
      archived: false,
      score: score,
      authorProfileId: authorProfileId,
      author: msg.sender,
      subject: subject,
      reviewId: reviewCount,
      // solhint-disable-next-line not-rely-on-time
      createdAt: block.timestamp,
      comment: comment,
      metadata: metadata,
      attestationDetails: attestationDetails
    });
    emit ReviewCreated(score, msg.sender, attestationHash, subject, reviewCount, mockId);
    reviewCount++;
  }

  function _addReview(
    uint256 mockId,
    address subject,
    bool isAttestation,
    bytes32 attestationHash,
    IEthosProfile ethosProfile
  ) internal returns (uint256 subjectProfileId) {
    // if profileId does not exist for subject, create and record a "mock"
    if (mockId == 0) {
@>    subjectProfileId = ethosProfile.incrementProfileCount(
        isAttestation,
        subject,
        attestationHash
      );
    } else {
      subjectProfileId = mockId;
    }

    reviewIdsBySubjectProfileId[subjectProfileId].push(reviewCount);
  }
```
</details>

This review is added the mock profile.
```solidity
    reviewIdsBySubjectProfileId[subjectProfileId].push(reviewCount);
```

Later, this attestation can be also claimed by an user.

<details>
    <summary>createAttestation function code snippet</summary>

  ```solidity
    function createAttestation(
      uint256 profileId,
      uint256 randValue,
      AttestationDetails calldata attestationDetails,
      string calldata evidence,
      bytes calldata signature
    ) external whenNotPaused {
      validateAndSaveSignature(
        _keccakForCreateAttestation(
          profileId,
          randValue,
          attestationDetails.account,
          attestationDetails.service,
          evidence
        ),
        signature
      );
  
      bytes32 hashStr = getServiceAndAccountHash(
        attestationDetails.service,
        attestationDetails.account
      );
  
      bool isClaimed = _claimAttestation(profileId, hashStr, evidence);
      if (isClaimed) {
        return;
      }
  
      bool isRestore = restoreIfArchived(hashStr);
      if (isRestore) {
        return;
      }
  
      _attestationShouldNotExist(hashStr);
  
      address ethosProfile = _getEthosProfile();
  
      // ensure specified profile is active
      (bool profileExists, ) = ITargetStatus(ethosProfile).targetExistsAndAllowedForId(profileId);
      if (!profileExists) {
        revert ProfileNotFound(profileId);
      }
  
      // ensure profile exists for sender address
      uint256 verifiedProfileId = IEthosProfile(ethosProfile).verifiedProfileIdForAddress(msg.sender);
      // ensure the requested attestation profile is the same as the sender's verified profile
      if (verifiedProfileId != profileId) {
        revert AddressNotInProfile(msg.sender, profileId);
      }
  
      attestationHashesByProfileId[profileId].push(hashStr);
      hashIndexByProfileIdAndHash[profileId][hashStr] =
        attestationHashesByProfileId[profileId].length -
        1;
  
      attestationByHash[hashStr] = Attestation({
        archived: false, 
        attestationId: attestationCount,
        createdAt: block.timestamp,
        profileId: profileId,
        account: attestationDetails.account,
        service: attestationDetails.service
      });
      attestationById[attestationCount] = attestationByHash[hashStr];
  
      // keep the profile contract up to date re: registered attestations
      IEthosProfile(ethosProfile).assignExistingProfileToAttestation(hashStr, profileId);
  
      emit AttestationCreated(
        profileId,
        attestationDetails.service,
        attestationDetails.account,
        evidence,
        attestationCount
      );
      attestationCount++;
    }
  ```
</details>

But after claiming the attestation, the reviews will be stuck on mock profile because we don't store the review on attestation.

### Impact

Medium - The written reviews will be unreachable for that attestation. They should be forwarded through new user profile but instead it's still stored in mock profile

### Mitigation

If the review is for attestation, also store the review in attestation related state variable.